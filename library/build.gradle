plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-parcelize'
    id 'maven-publish'
}

final VERSION_MAJOR = 0
final VERSION_MINOR = 0
final VERSION_PATCH = 1

android {
    namespace 'com.amary.amarywebview'
    compileSdk 33

    defaultConfig {
        minSdk 21
        targetSdk 33
        versionCode getVersionCode(VERSION_MAJOR, VERSION_MINOR, VERSION_PATCH)
        versionName getVersionName(VERSION_MAJOR, VERSION_MINOR, VERSION_PATCH)
        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = 'com.github.amary21'
            artifactId = 'amarywebview-android'
            version = getVersionName(VERSION_MAJOR, VERSION_MINOR, VERSION_PATCH)

            afterEvaluate {
                from components.release
            }
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.8.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    debugImplementation 'com.github.chuckerteam.chucker:library:3.5.2'
    releaseImplementation 'com.github.chuckerteam.chucker:library-no-op:3.5.2'
    implementation 'com.squareup.okhttp3:okhttp-urlconnection:4.10.0'
}

private static int getVersionCode(int major, int minor, int patch){
    return major * 100000 + minor * 10000  + patch
}

private static String getVersionName(int major, int minor, int patch){
    return "${major}.${minor}.${patch}"
}